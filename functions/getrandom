# Generate random numbers using the Collatz-Weyl Generator (CWG)
# Use 'getrandom --help' for more information.

local -A _fn=(
    [info]="Generate random numbers using Collatz-Weyl Generator"
    [desc]="Returns random integers using the CWG algorithm. Can output raw 64-bit
            numbers or constrained to a range. Supports multiple output formats."
    [version]="${_CWG_VERSION:-0.5.0}"
    [created]="2025"
    [modified]="2026-02-02"
    [license]="MIT"
    [author]="Andrzej Barabasz"
    [notes]="Requires cwg_next() to be defined. Range endpoints are inclusive."
)

local -a _fn_args=(
    "min|Minimum value (or max if single arg)|o|integer"
    "max|Maximum value|o|integer"
)

local -a _fn_opts=(
    "binary|b|Output in binary (base 2)"
    "hex|x|Output in hexadecimal (base 16)"
    "octal|o|Output in octal (base 8)"
)

local -a _fn_examples=(
    "getrandom|Raw 64-bit random number"
    "getrandom 100|Random number in range [0, 100]"
    "getrandom 10 20|Random number in range [10, 20]"
    "getrandom -x|Raw number in hexadecimal"
    "getrandom -b 255|Binary number in range [0, 255]"
)

local -A opts=() args=()
_fn_init "$@" || return $REPLY

# Calculate result
local result_val

if [[ -z "${args[min]}" ]]; then
    # No args: raw number
    cwg_next
    result_val=$REPLY
elif [[ -z "${args[max]}" ]]; then
    # One arg: range [0, min] (min is actually max here)
    local max=${args[min]}
    cwg_next
    (( result_val = REPLY % (max + 1) ))
else
    # Two args: range [min, max]
    local min=${args[min]}
    local max=${args[max]}
    if (( min > max )); then
        printe "Max ($max) must be greater than or equal to min ($min)."
        return 1
    fi
    local range
    (( range = max - min + 1 ))
    cwg_next
    (( result_val = (REPLY % range) + min ))
fi

# Format output
if (( ${+opts[binary]} )); then
    print -- $(( [#2] result_val ))
elif (( ${+opts[octal]} )); then
    print -- $(( [#8] result_val ))
elif (( ${+opts[hex]} )); then
    print -- $(( [#16] result_val ))
else
    print -- $(( [#10] result_val ))
fi
