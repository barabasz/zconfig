# Rename font files based on their internal font names
# Use 'rename-fonts --help' for more information.

local -A _fn=(
    [info]="Rename font files based on their internal font names"
    [desc]="Scans the current directory for .otf and .ttf font files and renames
            them using the font name stored in the file metadata."
    [version]="0.5.0"
    [created]="2025"
    [modified]="2026-02-02"
    [license]="MIT"
    [author]="Andrzej Barabasz"
    [notes]="Requires exiftool to be installed. Operates on the current directory only."
)

local -A opts=() args=()
_fn_init "$@" || return $REPLY

# Check dependency
(( ${+commands[exiftool]} )) || {
    printe "exiftool not found in PATH"
    return 1
}

setopt localoptions nullglob

local -a fonts=(*.otf *.ttf .*.otf .*.ttf)

if (( ${#fonts} == 0 )); then
    printi "No font files found in the current directory."
    return 0
fi

local -i count=0
local file name filename extension

for file in $fonts; do
    [[ -f "$file" ]] || continue

    # Try fontname first, then Font Name
    name=$(exiftool -fontname -S "$file" | sed 's/^[^:]*: //')
    if [[ -z "$name" ]]; then
        name=$(exiftool "$file" | grep "Font Name" | head -1 | sed 's/^[^:]*: //')
    fi

    if [[ -z "$name" ]]; then
        printw "No font name found for $file"
        continue
    fi

    filename="${file:t:r}"
    extension="${file##*.}"

    print "${b}${file}${x} ${y}â†’${x} ${g}${name}.${extension}${x}"
    mv "$file" "${name}.${extension}"
    (( count++ ))
done

print "\n${count} file(s) renamed."
