# Display disk usage statistics for a specific directory or mount point
# Use 'diskinfo --help' for more information.

local -A _fn=(
    [info]="Display disk usage statistics"
    [version]="0.5.0"
    [created]="2025"
    [modified]="2026-02-02"
    [license]="MIT"
    [author]="Andrzej Barabasz"
)

local -a _fn_args=(
    "path|Directory or mount point to analyze|o"
)

local -a _fn_examples=(
    "diskinfo|Show stats for current directory"
    "diskinfo /|Show stats for root filesystem"
    "diskinfo ~/Documents|Show stats for Documents"
)

local -A opts=() args=()
_fn_init "$@" || return $REPLY

local target="${args[path]:-.}"

# Check dependencies
(( ${+functions[get_disk_total]} )) || { printe "Missing get_disk_total()"; return 1; }
(( ${+functions[format_bytes]} ))  || { printe "Missing format_bytes()"; return 1; }

# Retrieve data
local total=$(get_disk_total "$target")

# If total is 0 or empty, the path is invalid
if [[ -z "$total" || "$total" == "0" ]]; then
    printe "Invalid path: $target"
    return 1
fi

local used=$(get_disk_used "$target")
local free=$(get_disk_free "$target")

# Calculate percentages
local used_pct=$(( (used * 100) / total ))
local free_pct=$(( (free * 100) / total ))

# Get filesystem/device name
local df_output=$(command df -P "$target" 2>/dev/null)
local last_line="${${(@f)df_output}[-1]}"
local device_name="${last_line%% *}"

printc "Disk Info: $device_name" $y
printkv "Total" "$(format_bytes $total)"
printkv "Used"  "$(format_bytes $used) ($used_pct%)" $y
printkv "Free"  "$(format_bytes $free) ($free_pct%)" $g
