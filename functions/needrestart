# Manage needrestart interactive prompts on Ubuntu
# Use 'needrestart --help' for more information.

local -A _fn=(
    [info]="Manage needrestart interactive prompts on Ubuntu"
    [desc]="Disables or enables the interactive restart prompt during apt operations.
            Modifies /etc/needrestart/needrestart.conf to control whether apt asks
            about restarting services after package updates."
    [version]="0.5.0"
    [created]="2025"
    [modified]="2026-02-02"
    [license]="MIT"
    [author]="Andrzej Barabasz"
    [notes]="Ubuntu only. Requires sudo for modifying the config file."
)

local -a _fn_opts=(
    "disable|d|Disable prompts (auto-restart services)"
    "enable|e|Enable prompts (ask before restart)"
)

local -a _fn_examples=(
    "needrestart -d|Disable interactive prompts"
    "needrestart -e|Enable interactive prompts"
)

local -A opts=() args=()
_fn_init "$@" || return $REPLY

# Ubuntu only
is_ubuntu || {
    printe "This command is only available on Ubuntu."
    return 1
}

local conf="/etc/needrestart/needrestart.conf"

# Check if config exists
[[ -f $conf ]] || {
    printe "Config file not found: $conf"
    return 1
}

if (( ${+opts[disable]} )); then
    sudo sed -i -e "s/#\$nrconf{restart} = 'i';/\$nrconf{restart} = 'a';/g" "$conf"
    printi "Interactive prompts disabled (auto-restart enabled)."
    return 0
fi

if (( ${+opts[enable]} )); then
    sudo sed -i -e "s/\$nrconf{restart} = 'a';/#\$nrconf{restart} = 'i';/g" "$conf"
    printi "Interactive prompts enabled."
    return 0
fi

# No option provided - show usage
printe "No action specified. Use --disable or --enable."
_fn_usage >&2
return 1
